plugins {
    id "com.github.triplet.play" version "2.1.0"
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'io.fabric'
}
apply from: "$project.rootDir/distribution/keys.gradle"

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "br.com.yves.groupmatch"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode application.versionCode
        versionName application.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            keyAlias KEY_ALIAS
            storePassword STORE_PASSWORD
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [appName: "GroupMatch"]
        }
        debug {
            applicationIdSuffix '.debug'
            manifestPlaceholders = [appName: "GroupMatch Debug"]
        }
    }
}

play {
    serviceAccountCredentials = file("$project.rootDir/distribution/gradle_play_publisher_key.json")
    defaultToAppBundles = true
    track = "internal"
    releaseStatus = "draft"
}

kapt {
    generateStubs = true
}

dependencies {
    def dependencies = rootProject.ext.dependencies
    def testDependencies = rootProject.ext.testDependencies

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':data')
    implementation project(path: ':domain')

    implementation dependencies.kotlin
    implementation dependencies.support_v4
    implementation dependencies.app_compat
    implementation dependencies.design
    implementation dependencies.recycler_view
    implementation dependencies.card_view
    implementation dependencies.constraint_layout
    implementation dependencies.lifecycle
    implementation dependencies.navigation_fragment
    implementation dependencies.navigation_ui
    implementation dependencies.anko
    implementation dependencies.gson
    implementation dependencies.coroutines
    implementation dependencies.coroutines_core
    implementation dependencies.firebase
    implementation dependencies.crashlytics

    // Tests
    //FIXME: Mutreta para usar JSR-310 nos testes unit√°rios. Deveria mockar.
    testImplementation('org.threeten:threetenbp:1.3.2') {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }
    testImplementation testDependencies.junit
    testImplementation testDependencies.mockitoKotlin
}
apply plugin: 'com.google.gms.google-services'